<MENU> 		 ::= "menu" <ID> '{' <NOMBRE> {<INGREDIENTE> ';'} <PLATO> {<PLATO>} '}'
<PLATO> 	 ::= "plato" <ID> '{' <CABECERA> {<INGREDIENTE> ';'} {<INSTRUCCION>} '}'

<CABECERA> 	 ::= "info" '{' <NOMBRE> <USUARIO> <RACIONES> <TIEMPO_TOTAL> [<CALORIAS>] [<TIPO>] [<COCINA>] '}'
<NOMBRE> 	 ::= "nombre" ':' <CADENA> ';'
<USUARIO> 	 ::= "usuario" ':' <ID> ';' 
<RACIONES> 	 ::= "raciones" ':' <NUMERO> ';'
<TIEMPO_TOTAL> 	 ::= "tiempo" ':' <DURACION> ';' 
<CALORIAS> 	 ::= "calorias" ':' <NUMERO> ';'
<TIPO> 		 ::= "tipo" ':' <STRING> ';'
<COCINA> 	 ::= "cocina" ':' <STRING> ';'

<INSTRUCCION>  	 ::= (<PROGRAMAR> | <ANADIR> | <CALENTAR> | <REMOVER> | <ACCION_USUARIO> | <SACAR>) ';'
<PROGRAMAR>   	 ::= "programar" '(' <TEMPERATURA> ',' <VELOCIDAD> ',' <TIMER> [',' "inverso"] ')'
<CALENTAR>       ::= "calentar" '(' <TEMPERATURA> ',' <TIMER> ')'
<REMOVER>        ::= "remover" '(' <VELOCIDAD> ',' <TIMER> [',' "inverso"] ')'
<ACCION_USUARIO> ::= <STRING>
<ANADIR>         ::= "anadir" '(' (<INGREDIENTE> | <STRING>) ')'
<SACAR>          ::= "sacar" '(' <STRING> ')'
<TEMPERATURA>    ::= (<TEMP> | "varoma")
<VELOCIDAD>      ::= (<NUMERO> | "cuchara" | "espiga" | "turbo")
<INGREDIENTE>    ::= <CANTIDAD> <STRING>
<CANTIDAD>       ::= (<NUMERO> ('l' | 'ml' | 'g' | 'cucharada' | 'ud') | "al_gusto")

<ID>             ::= ('A' | ... | 'Z' | 'a' | ... | 'z') {('A' | ... | 'Z' | 'a' | ... | 'z'  | '0' | ... | '9')}
<STRING> 	 ::= '"' {('A' | ... | 'Z' | 'a' | ... | 'z' | '0' | ... | '9' | <BLANCO>)} '"'

<DURACION>       ::= [<NUMERO> 'h'] <NUMERO> 'm'
<TIMER>   	 ::= <DIGITO> <DIGITO> ':' <DIGITO> <DIGITO>
<TEMP>           ::= <NUMERO> [º] 'C'
<DIGITO>         ::= ('0' | ... | '9')
<NUMERO>         ::= <DIGITO> {<DIGITO>}
<BLANCO>         ::= (' ' | '\t' | '\n' | '\r')




